plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.0-SNAPSHOT'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'org.openapi.generator' version '7.2.0'
}

group = 'com.gsuleimanov'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
	implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
	implementation 'io.swagger.core.v3:swagger-annotations:2.2.20'
	implementation 'org.liquibase:liquibase-core'

	runtimeOnly 'com.h2database:h2'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

openApiValidate {
	inputSpec = "$rootDir/src/main/resources/openapi/expenses-api.yaml"
	recommend = true
}

openApiGenerate {
	generatorName = "spring"
	inputSpec = "$rootDir/src/main/resources/openapi/expenses-api.yaml"
	outputDir = "$buildDir/generated/openapi"
	apiPackage = "com.gsuleimanov.expenses.api"
	modelPackage = "com.gsuleimanov.expenses.model"
	configOptions = [
		modelNameSuffix: "Dto",
		apiNameSuffix: "Controller",
		generateApiTests: "true",
		dateLibrary: "java8",
		interfaceOnly: "true",
		skipDefaultInterface: "true",
		useSpringBoot3: "true",
		useTags: "true",
		documentationProvider: "springdoc",
		annotationLibrary: "swagger2",
		serializationLibrary: "jackson"
	]
}

sourceSets {
	main {
		java {
			srcDirs += [
				"${buildDir}/generated/openapi/src/main/java"
			]
		}
	}
}

tasks.compileJava {
	dependsOn tasks.openApiGenerate
}

clean {
	delete "${buildDir}/generated"
}
